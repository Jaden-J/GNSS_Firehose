	constant uart_rx_data, 20
	constant uart_rx_ready, 21
	constant uart_rx_read, 21

	constant uart_tx_data, 20
	constant uart_tx_ready, 22
	constant uart_tx_write, 22

	constant port_out_clock, 00
	constant port_out_phy_smi, 04
	constant port_out_pwm, 06
	constant port_i2c, 11

	constant max2112_i2c_slave_addr, 60

main:   call hw_init
loop:   call uart_service
	call agc_service
	jump loop

hw_init:
	call clock_init
	load s0,1             ; ADC channel 1
	call adc_init
;	load s0,2             ; ADC channel 2
;	call adc_init
	load s0,3             ; ADC channel 3
	call adc_init
	load s0,4             ; ADC channel 4
	call adc_init
	call phy_setup
	load s0, 1            ; RF channel 1
	load s1, 41           ; N=65 (L1)
	call max2112_init
;	load s0, 2            ; RF channel 2
;	load s1, 31           ; N=49 (L5)
;	call max2112_init
	load s0, 3            ; RF channel 3
	load s1, 33           ; N=51 (L2)
	call max2112_init
	load s0, 1            ; RF channel 1
	load s1, 00           ; gc=120 (0x0078)
	load s2, 78
	call set_agc
;	load s0, 2            ; RF channel 2
;	load s1, 00           ; gc=120 (0x0078)
;	load s2, 78
;	call set_agc
	load s0, 3            ; RF channel 3
	load s1, 00           ; gc=120 (0x0078)
	load s2, 78
	call set_agc
	return

uart_service:
	input s0, uart_rx_ready
	compare s0, 00
	return z
	call getchar
	compare s0, 6d   ; 'm'
	jump nz, skip1
	call port_addr
	return
skip1:  compare s0, 77   ; 'w'
	jump nz, skip2
	call port_write
	return
skip2:  compare s0, 72   ; 'r'
	jump nz, skip3
	call port_read
	return
skip3:	compare s0, 78   ; 'x'
	jump nz, skip4
	call port_echo
	return
skip4:  and s0, 0f
	sl0 s1
	sl0 s1
	sl0 s1
	sl0 s1
	or s1, s0
	return

port_addr:
	load s2, s1
	return

port_write:
	output s1, (s2)
	return

port_read:
	input s0, (s2)
	call putchar
        return

port_echo:
	load s0, s2
	call putchar
	return

getchar:
	input sF, uart_rx_ready
	compare sF, 00
	jump z, getchar
	input s0, uart_rx_data
	load sF, 01
	output sF, uart_rx_read
	load sF, 00
	output sF, uart_rx_read
	return

putchar:
	input sF, uart_tx_ready
	compare sF, 00
	jump z, putchar
	output s0, uart_tx_data
	load sF, 01
	output sF, uart_tx_write
	load sF, 00
	output sF, uart_tx_write
	return

;fixme
agc_service:
	return

clock_init: 
	load s0, 80
	load s1, 02
	load s2, 0d
	load s3, 20
	call clock_write		; 80020d20
	load s0, 00
	load s1, 00
	call clock_write		; 00000d20
	call clock_write		; 00000d20
	load s3, 21
	call clock_write		; 00000d21
	call clock_write		; 00000d21
	load s0, 80
	load s3, 22
	call clock_write		; 80000d22
	call clock_write		; 80000d22
	load s0, 00
	load s1, 00
	load s2, 05
	load s3, 03
	call clock_write		; 00000503
	call clock_write		; 00000503
	load s3, 04
	call clock_write		; 00000504
	call clock_write		; 00000504
	load s3, 05
	call clock_write		; 00000505
	call clock_write		; 00000505
	load s0, 06
	load s1, 66
	load s2, 00
	load s3, 06
	call clock_write		; 06660006
	load s0, 01
	load s1, 00
	load s2, 00
	load s3, 07
	call clock_write		; 01000007
	load s0, 11
	load s1, 11
	load s2, 00
	load s3, 08
	call clock_write		; 11110008
	load s0, 55
	load s1, 55
	load s2, 55
	load s3, 49
	call clock_write		; 55555549
	load s0, 10
	load s1, 00
	load s2, 40
	load s3, 0a
	call clock_write		; 1000400a
	load s0, 34
	load s1, 00
	load s2, 00
	load s3, 0b
	call clock_write		; 3400000b
	load s0, 13
	load s1, 0c
	load s2, 00
	load s3, 6c
	call clock_write		; 130c006c
	load s0, 3b
	load s1, 02
	load s2, 06
	load s3, 6d
	call clock_write		; 3b02066d
	load s0, 02
	load s1, 00
	load s2, 00
	load s3, 0e
	call clock_write		; 0200000e
	load s0, c1
	load s1, 55
	load s2, 04
	load s3, 10
	call clock_write		; c1550410
	load s0, 00
	load s1, 00
	load s2, 00
	load s3, 18
	call clock_write		; 00000018
	load s0, 83
	load s1, a8
	load s2, 00
	load s3, 1a
	call clock_write		; 83a8001a
	load s0, 00
	load s1, 10
	load s2, 00
	load s3, 1c
	call clock_write		; 0010001c
	load s0, 00
	load s1, 80
	load s2, 04
	load s3, 1d
	call clock_write		; 0080041d
	load s0, 02
	load s1, 00
	load s2, 04
	load s3, 1e
	call clock_write		; 0200041e
	call delay_100ms
	load s0, 34
	load s1, 01
	load s2, 00
	load s3, 0b
	call clock_write		; 3401000b
	call delay_100ms
	load s1, 00
	call clock_write		; 3400000b
	call delay_100ms
	return

clock_write:
	load s4, s0
	load s5, 0
cw0:	sla s4
	call clock_write_bit
	add s5, 1
	compare s5, 8
	jump nz, cw0
	load s4, s1
	load s5, 0
cw1:	sla s4
	call clock_write_bit
	add s5, 1
	compare s5, 8
	jump nz, cw1
	load s4, s2
	load s5, 0
cw2:	sla s4
	call clock_write_bit
	add s5, 1
	compare s5, 8
	jump nz, cw2
	load s4, s3
	load s5, 0
cw3:	sla s4
	call clock_write_bit
	add s5, 1
	compare s5, 8
	jump nz, cw3
	call clock_le
	return

clock_write_bit:
	jump c, bit_h
	load s8, 00
	load s9, 04
	jump bit_w
bit_h:	load s8, 02
	load s9, 06
bit_w:	output s8, port_out_clock
	call delay_5us
	output s9, port_out_clock
	call delay_5us
	output s8, port_out_clock
	call delay_5us
	return

clock_le:
	load s8, 00
	load s9, 01
	output s8, port_out_clock
	call delay_5us
	output s9, port_out_clock
	call delay_5us
	output s8, port_out_clock
	call delay_5us
	return

adc_init:
	load s1, 1
	call adc_cs
	load s2, 0
	load s3, 03
	call adc_write
	load s2, 1
	load s3, 02
	call adc_write
	load s2, 3
	load s3, 06
	call adc_write
	load s2, 4
	load s3, 00
	call adc_write
	load s2, 5
	load s3, 00
	call adc_write
	load s2, 6
	load s3, 08
	call adc_write
	load s2, 8
	load s3, 88
	call adc_write
	return

adc_cs:
	load s8, 0b
	add s8, s0
	compare s1, 00
	jump nz, cs_hi
	load s9, 01
	jump cs_w
cs_hi:	load s9, 09
cs_w:	output s9, (s8)
	call delay_5us
	return

adc_write:
	load s1, 0
	call adc_cs
	load s1, 0
	sla s1
	call adc_write_bit
	load sA, 0
	load sB, s2
	sla sB
aw1:	sla sB
	call adc_write_bit
	add sA, 1
	compare sA, 7
	jump nz, aw1
	load sA, 0
	load sB, s3
aw2:	sla sB
	call adc_write_bit
	add sA, 1
	compare sA, 8
	jump nz, aw2
	load s1, 1
	call adc_cs
	return

adc_write_bit:
	jump c, abit_h
	load s8, 01
	load s9, 05
	jump abit_w
abit_h:	load s8, 03
	load s9, 07
abit_w:	load sC, 0b
	add sC, s0
	output s8, (sC)
	call delay_5us
	output s9, (sC)
	call delay_5us
	output s8, (sC)
	call delay_5us
	return

phy_setup:
	load s0, 1c		; address 28
	load s1, f0		; value 0xf000
	load s2, 00
	call phy_smi_write_extended
	return

phy_smi_write_extended:
	load s3, 1f
	load s4, 00
	load s5, 01
	call phy_smi_write
	load s3, s0
	load s4, s1
	load s5, s2
	call phy_smi_write
	load s3, 1f
	load s4, 00
	load s5, 00
	call phy_smi_write
	return

phy_smi_write:
	call phy_smi_write_0
	call phy_smi_write_1
	call phy_smi_write_0
	call phy_smi_write_1
	call phy_smi_write_0
	call phy_smi_write_0
	call phy_smi_write_0
	call phy_smi_write_0
	call phy_smi_write_0
	load s6, s3
	sla s6
	sla s6
	sla s6
	load s7, 0
p1:	sla s6
	call phy_smi_write_bit
	add s7, 1
	compare s7, 5
	jump nz, p1
	call phy_smi_write_1
	call phy_smi_write_0
	load s6, s4
	load s7, 0
p2:	sla s6
	call phy_smi_write_bit
	add s7, 1
	compare s7, 8
	jump nz, p2
	load s6, s5
	load s7, 0
p3:	sla s6
	call phy_smi_write_bit
	add s7, 1
	compare s7, 8
	jump nz, p3
	return

phy_smi_write_0:
	load s6, 0
	sla s6
	call phy_smi_write_bit
	return

phy_smi_write_1:
	load s6, 80
	sla s6
	call phy_smi_write_bit
	return

phy_smi_write_bit:
	jump c, pw_hi
	load s8, 01
	load s9, 05
	jump pw_w
pw_hi:	load s8, 03
	load s9, 07
pw_w:	output s8, port_out_phy_smi
	call delay_5us
	output s9, port_out_phy_smi
	call delay_5us
	output s8, port_out_phy_smi
	call delay_5us
	return

max2112_init:
	call i2c_init
	load s2, 05
	load s3, 01
	call i2c_write
	load s2, 06
	load s3, 40
	call i2c_write
	load s2, 07
	load s3, cc
	call i2c_write
	load s2, 08
	load s3, 55
	call i2c_write
	load s2, 09
	load s3, 06
	call i2c_write
	load s2, 0a
	load s3, 00
	call i2c_write
	load s2, 0b
	load s3, 08
	call i2c_write
	load s2, 01
	load s3, s1
	call i2c_write
	load s2, 02
	load s3, 10
	call i2c_write
	load s2, 03
	load s3, 00
	call i2c_write
	load s2, 04
	load s3, 00
	call i2c_write
	load s2, 00
	load s3, 00
	call i2c_write
	return

i2c_init:
	load s4, 1
	call i2c_set_sda
	load s4, 1
	call i2c_set_scl
	return

; write a byte to a MAX2112 register
;
; s0: RF channel number
; s2: MAX2112 register address
; s3: data byte

i2c_write:
	call i2c_start
	load s5, max2112_i2c_slave_addr
	sl0 s5
	load s6, 7
	call i2c_bits
	load s9, 0
	call i2c_bit
	call i2c_ack
	load s5, s2
	load s6, 8
	call i2c_bits
	call i2c_ack
	load s5, s3
	load s6, 8
	call i2c_bits
	call i2c_ack
	call i2c_stop
	return

; Set the I2C signals
;
; s0: RF channel number
; s4: bit value

i2c_set_sda:
	load sF, s0
	sub sF, 1
	add sF, port_i2c
	input sE, (sF)
	load sD, s4
	sl0 sD
	and sE, fd
	or sE, sD
	output sE, (sF)
	call delay_5us
	return

i2c_set_scl:
	load sF, s0
	sub sF, 1
	add sF, port_i2c
	input sE, (sF)
	and sE, fe
	or sE, s4
	output sE, (sF)
	call delay_5us
	return

; write a bit to I2C
;
; s0: RF channel number
; s9: bit value

i2c_bit:
	load s4, s9
	call i2c_set_sda
	load s4, 1
	call i2c_set_scl
	load s4, 0
	call i2c_set_scl
	return

; write a sequence of bits to I2C
;
; s0: RF channel number
; s5: data
; s6: bit count

i2c_bits:
	load sA, 0
ibloop:	sla s5
	jump c, i_hi
	load s9, 0
	jump i_wb
i_hi:	load s9, 1
i_wb:	call i2c_bit
	add sA, 1
	compare sA, s6
	jump nz, ibloop
	return

i2c_ack:
	call i2c_get_bit
	return

i2c_start:
	load s4, 0
	call i2c_set_sda
	load s4, 0
	call i2c_set_scl
	return

i2c_stop:
	load s4, 1
	call i2c_set_scl
	load s4, 1
	call i2c_set_sda
	return

i2c_get_bit:
	load s4, 1
	call i2c_set_sda
	load s4, 1
	call i2c_set_scl
;read bit from sda
	load s4, 0
	call i2c_set_scl
	return

set_agc:
	load sF, s0
	sub sF, 1
	sl0 sF
	add sF, port_out_pwm
	output s2, (sF)		; LS byte
	add sF, 1
	output s1, (sF)		; MS byte
	return

delay_5us:
	load sF, 0
dl0:	add sF, 01
	compare sF, 1a   ; 26 iterations
	jump nz, dl0
	return

delay_40us:
	load sF, 0
dl1:	add sF, 01
	compare sF, d0   ; 208 iterations
	jump nz, dl1
	return

delay_10ms:
	load sE, 0
dl2:	call delay_40us
	add sE, 01
	compare sE, fa   ; 250 iterations
	jump nz, dl2
	return

delay_100ms:
	load sD, 0
dl3:	call delay_10ms
	add sD, 01
	compare sD, 0a   ; 10 iterations
	jump nz, dl3
	return
